// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.5.31'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.19.0"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

gradle.projectsEvaluated {
    def lintConfigPath = "${rootProject.projectDir}/lint.xml"
    subprojects {
        if (plugins.hasPlugin("com.android.application") || plugins.hasPlugin("com.android.library")) {
            extensions.getByName("android")["lintOptions"].with {
                abortOnError true
                warningsAsErrors true
                baseline file("lint-baseline.xml")
                lintConfig file(lintConfigPath)
            }
        }
    }
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"

    detekt {
        autoCorrect = true
        buildUponDefaultConfig = true
        config = files("${rootProject.rootDir}/config/detekt/detekt-config.yml")
        baseline = file("detekt-baseline.xml")
        parallel = true

        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    dependencies {
        detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:1.19.0")
    }
}
