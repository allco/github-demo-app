// Top-level build file where you can add configuration options common to all sub-projects/modules.

import se.allco.githubbrowser.buildsrc.Libs

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Detekt.gradlePlugin
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

gradle.projectsEvaluated {
    def lintConfigPath = "${rootProject.projectDir}/config/lint.xml"
    subprojects {
        if (plugins.hasPlugin("com.android.application") || plugins.hasPlugin("com.android.library")) {
            extensions.getByName("android")["lintOptions"].with {
                abortOnError true
                warningsAsErrors true
                baseline file("lint-baseline.xml")
                lintConfig file(lintConfigPath)
                disable 'GradleDependency', 'OldTargetApi'
            }
        }
    }
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"

    detekt {
        autoCorrect = true
        buildUponDefaultConfig = true
        config = files("${rootProject.rootDir}/config/detekt/detekt-config.yml")
        baseline = file("detekt-baseline.xml")
        parallel = true

        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    dependencies {
        detektPlugins(Libs.Detekt.formatting)
    }
}
